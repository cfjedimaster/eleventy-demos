---
layout: post
title: "Ask a Jedi: Adding additional text to a CFCHART"
date: "2010-01-25T09:01:00+06:00"
categories: [coldfusion]
tags: []
banner_image: 
permalink: /2010/01/25/Ask-a-Jedi-Adding-additional-text-to-a-CFCHART
guid: 3695
---

Alison asks:

<blockquote>
Have you ever had to include a "Source: the name of a company" within a ColdFusion chart, at the bottom of the chart?  I would like it to be part of the chart image so that when someone copies the chart, the Source is there. 
</blockquote>

<p>

Nope, I haven't, but that won't stop me from trying! So the first thing I did was check the built in chart editor. I thought perhaps they may have some kind of arbitrary way to add text to an image. While there is a title, a legend, and marks, there doesn't seem to be support for additional blocks of text. I could be wrong, but from what I see, there is no XML way to do this. So I then switched to ColdFusion's image functions. I figured we could simply treat the chart as an image and then edit it to add the text. This is what I came up with.
<!--more-->
<p>

First, begin with a simple chart. Notice that I use the name attribute. This stores the binary data into a ColdFusion variable.

<p>

<code>
&lt;cfchart chartheight="500" chartwidth="500" title="Test Chart" format="png" name="mychart"&gt;

	&lt;cfchartseries type="bar"&gt;
		&lt;cfchartdata item="Apples" value="50"&gt;
		&lt;cfchartdata item="Bananas" value="40"&gt;
		&lt;cfchartdata item="Cherries" value="72"&gt;
		&lt;cfchartdata item="Donuts" value="59"&gt;
	&lt;/cfchartseries&gt;
	
&lt;/cfchart&gt;
</code>

<p>

Now that we have the binary data, we need to re-"cast" it as a native ColdFusion Image object:

<p>

<code>
&lt;!--- rewrap as an image ---&gt;
&lt;cfset myChart = imageNew(mychart)&gt;
</code>

<p>


I confirmed that everything was kosher with a quick cfdump. It confirmed I now had an image object. So the next step may not be so obvious. While you can resize an image, doing so may reduce the quality of the image. I decided instead to create a new, blank canvas. I then pasted the chart into it. I added 35 to the height of the new canvas to create room for our text. You can skip this step and try resizing, but be sure to keep the width the same and just increase the height.

<p>

<code>
&lt;!-- create new image with more space on the bottom ---&gt;
&lt;cfset newCanvas = imageNew("", mychart.width, mychart.height + 35, "rgb", "white")&gt;

&lt;!--- paste in the old one ---&gt;
&lt;cfset imagePaste(newCanvas, myChart, 0, 0)&gt;
</code>

<p>

Ok, so the final step is to just write the text to the image. I picked a random font, color, and size. Change to your liking:

<p>

<code>
&lt;cfset textProps = {% raw %}{font="Arial-Black", size="20"}{% endraw %}&gt;
&lt;cfset imageSetDrawingColor(newCanvas, "black")&gt;
&lt;cfset imageDrawText(newCanvas, "Charts generated by monkeys #chr(169)##year(now())#", 1, newCanvas.height-20, textProps)&gt;
</code>

<p>

Notice I place the text at "height of canvas - 20". This was purely arbitrary. You may want to tweak these values a bit to place the text differently. ColdFusion does not provide a simple way to handle centering text, or to even get the size of the text. Luckily, there's a CFC for that: <a href="http://imageutils.riaforge.org/">ImageUtils</a>. But for now, the simple, hard coded placement works well:

<p>

<img src="https://static.raymondcamden.com/images/Screen shot 2010-01-25 at 9.07.25 AM.png" title="I've got mad chart skills. Don't hate me for it." />

<p>

Here is the entire test script. Enjoy!

<code>

&lt;cfchart chartheight="500" chartwidth="500" title="Test Chart" format="png" name="mychart"&gt;

	&lt;cfchartseries type="bar"&gt;
		&lt;cfchartdata item="Apples" value="50"&gt;
		&lt;cfchartdata item="Bananas" value="40"&gt;
		&lt;cfchartdata item="Cherries" value="72"&gt;
		&lt;cfchartdata item="Donuts" value="59"&gt;
	&lt;/cfchartseries&gt;
	
&lt;/cfchart&gt;

&lt;!--- rewrap as an image ---&gt;
&lt;cfset myChart = imageNew(mychart)&gt;

&lt;!-- create new image with more space on the bottom ---&gt;
&lt;cfset newCanvas = imageNew("", mychart.width, mychart.height + 35, "rgb", "white")&gt;

&lt;!--- paste in the old one ---&gt;
&lt;cfset imagePaste(newCanvas, myChart, 0, 0)&gt;

&lt;cfset textProps = {% raw %}{font="Arial-Black", size="20"}{% endraw %}&gt;
&lt;cfset imageSetDrawingColor(newCanvas, "black")&gt;
&lt;cfset imageDrawText(newCanvas, "Charts generated by monkeys #chr(169)##year(now())#", 1, newCanvas.height-20, textProps)&gt;

&lt;cfimage action="writeToBrowser" source="#newCanvas#"&gt;
&lt;p&gt;
</code>