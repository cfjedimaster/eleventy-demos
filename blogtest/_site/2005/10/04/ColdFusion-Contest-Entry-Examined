<h1>Post</h1>

<p>The ColdFusion <a href="http://ray.camdenfamily.com/index.cfm/2005/9/20/Contest-Shall-We-Play-a-Game">contest</a> is now closed, and I am beginning to look at the entries. As I mentioned, I was going to pick the code apart and point out mistakes. This is <i>not</i> to make anyone feel bad or to make people think I don't make mistakes (anyone remember the supreme whopper?), but as readers of my blog know, I love to point out mistakes and issues because I think it helps people learn. Also, I like to point out alternative ways of coding functionality, just in case you don't know such methods exist. So, enough gab, let's get on with it.</p>
<p>The first entry can be viewed <a href="http://ray.camdenfamily.com/demos/contest1/entry1/guessanumber.cfm">here</a>. Play with it a few times before going on to see if you find the same issues I did.</p>
<!--more-->
<p>Now that you have tried it out, let's take a look at the code:</p>
<code>
&lt;cfset VARIABLES.thisPage = GetFileFromPath(GetTemplatePath())&gt;
&lt;cfset VARIABLES.lownumber = 1&gt;
&lt;cfset VARIABLES.topnumber = 100&gt;
<p>&lt;cfparam name=&quot;FORM.guess&quot; default=&quot;&quot;&gt;
&lt;cfparam name=&quot;FORM.guesses&quot; default=&quot;&quot;&gt;
&lt;cfparam name=&quot;FORM.thenumber&quot; default=&quot;#RandRange(lownumber,topnumber)#&quot;&gt;
&lt;cfparam name=&quot;VARIABLES.message&quot; default=&quot;&quot;&gt;</p>
<p>&lt;cfif FORM.guess IS NOT &quot;&quot;&gt;
&lt;cfset FORM.guesses = FORM.guesses &amp; FORM.guess &amp; &quot;,&quot;&gt;
&lt;cfif FORM.guess IS FORM.thenumber&gt;
&lt;cfset VARIABLES.message = &quot;That's it, good job! It took you #ListLen(FORM.guesses)# tries&quot;&gt;
&lt;cfelse&gt;
&lt;!--- let's see where the guess is in relation to the number ---&gt;
&lt;cfif FORM.guess GT FORM.thenumber&gt;
&lt;cfset VARIABLES.message = &quot;Your guess is too high&quot;&gt;
&lt;cfelseif FORM.guess LT FORM.thenumber&gt;
&lt;cfset VARIABLES.message = &quot;Your guess is too low&quot;&gt;
&lt;/cfif&gt;
&lt;/cfif&gt;
&lt;/cfif&gt;</p>
<p>&lt;cfoutput&gt;
I'm thinking of a number between #VARIABLES.lownumber# and #VARIABLES.topnumber#.&lt;br&gt;
Care to guess what it is?&lt;br&gt;
&lt;cfif FORM.guess IS NOT &quot;&quot;&gt;
&lt;b&gt;You guessed #FORM.guess#. #VARIABLES.message#&lt;/b&gt;&lt;br&gt;
So far you've guessed: #FORM.guesses#
&lt;/cfif&gt;
&lt;form action=&quot;#thispage#&quot; method=&quot;post&quot; style=&quot;width: 200px; text-align: right; &quot;&gt;
&lt;input type=&quot;hidden&quot; name=&quot;thenumber&quot; value=&quot;#FORM.thenumber#&quot;&gt;
&lt;input type=&quot;hidden&quot; name=&quot;guesses&quot; value=&quot;#FORM.guesses#&quot;&gt;
I think it's: &lt;input type=&quot;text&quot; name=&quot;guess&quot; size=&quot;3&quot; value=&quot;#FORM.guess#&quot;&gt;&lt;br&gt;
&lt;input type=&quot;submit&quot; value=&quot;take a guess&quot;&gt;&lt;br&gt;
&lt;input type=&quot;button&quot; value=&quot;try again&quot;  onClick=&quot;location.href='#thispage#';&quot;&gt;
&lt;/form&gt;
&lt;/cfoutput&gt;
</code></p>
<p>There are some interesting techniques in here that beginners should be aware of. First off, note how the author created a variable, thispage, to stand for the current page. He uses that variable later on in the action attribute of the form. This is a <b>great</b> idea, as it means you can rename the file without having to worry about the form action. I will point out one thing I'd change, though. Instead of:</p>
<code>
&lt;cfset VARIABLES.thisPage = GetFileFromPath(GetTemplatePath())&gt;
</code>
<p>I'd use:</p>
<code>
&lt;cfset VARIABLES.thisPage = cgi.script_name&gt;
</code>
<p>This will return a slightly different value. Instead of just &quot;foo.cfm&quot;, you will get the full path, &quot;/moo/goo/foo.cfm&quot;. Both work of course, but the cgi variable involves a bit less code.</p>
<p><b>Edited:</b> Well, it figures that if I wrote a post pointing out mistakes in code, I'd make a mistake myself. Barney points out in the comments that cgi.script_name will have issues on a server with a context root that isn't /. Turns out, the authors solution was better than my solution.</p>
<p>On a style note (not that I'm a style nazi), I noticed the author used the variables scope prefix, but forgot to use it again in both the form tag, and the &quot;Try Again&quot; button. In general, I do not use specify &quot;Variables&quot; when using the Variables scope, but if you do, you should try to use it consistently. Again, this isn't a huge deal, but something to keep in mind. Let's move on.</p>
<p>Notice what happens when you make a few mistakes? The author tracks your guesses, which is a nice touch, but the string is a bit messed up. Instead of seeing a list like &quot;50, 75&quot;, you have &quot;50,75,&quot;. Notice the trailing comma? This comes from this line:</p>
<code>
&lt;cfset FORM.guesses = FORM.guesses & FORM.guess & ","&gt;
</code>
<p>What should he have used? The <a href="http://livedocs.macromedia.com/coldfusion/7/htmldocs/00000547.htm#130310">listAppend</a> function:</p>
<code>
&lt;cfset form.guesses = listAppend(form.guesses, form.guess)&gt;
</code>
<p>ListAppend will automatically add the comma (or whatever delimiter) before adding the new entry.</p>
<p>Another mistake - when the user finally picks the winning number, the &quot;take a guess&quot; button still shows up. This could be fixed by simply checking to see if the user has &quot;won&quot;, and if so, hide the button.</p>
<p>So - now lets tackle the <b>bad</b> mistake. Have you guessed it yet? The code the author wrote does not make use of session or other RAM based variables. Instead, it keeps the state of the game in the form itself. That by itself isn't so bad, but the actual winning number is actually in the form. I discussed stuff like this in my last <a href="http://ray.camdenfamily.com/index.cfm/2005/9/14/Macrochat-Recording-and-Downloads">Macrochat</a>. Basically, you can't trust hidden form fields since they really aren't hidden. I don't mean to beat up on the author because security wasn't really important to this contest, but I want to use it as a warning to my readers in general. <b>Always think about security. Never stop. Period!</b> So, with that yelling out of the way (grin), what are some ways he could have gotten around it? Well, the <i>easiest</i> solution would be to move to RAM based variables and store the right answer (along with the guesses) in the Session scope. But let's say, for whatever reason, that isn't an option. There are a couple of things you could do. You could encrypt the value. It would still be visible to the user, but in encrypted form. Encryption is not perfect, but you need to weight the costs and benefits. If this game were for real money, then maybe that solution wouldn't work. But if it was just for fun, than it would be acceptable. Another option - create a UUID, which is a unique number, and store the right answer in a database table. The form would have the UUID, and you would simply look up the answer based on the UUID from the form. If the user changed the value for some reason (yes, Virginia, users can change form values if they want to be nasty), then the lookup would fail. You could log it, or simply generate them a new UUID. Do folks have other ideas?</p>
<p>So, thats it for the first entry. I have 5 or 6 more to go through before I pick a winner. Hopefully they will all be as interesting, and the author should feel proud. These are all &quot;normal&quot; mistakes, not &quot;Oh my god, we hired a muppet&quot; mistakes.</p>
}