<h1>Post</h1>

<p>Today I get to talk about my favorite Transfer topic (although events may supplant it) - Decorators. No, I'm not talking about the people who come in and hang wallpaper or rearrange your furniture. Rather, decorators in Transfer allow you to extend and build upon the TransferObjects automatically created for you when you get data via Transfer.</p>
<!--more-->
<p>If you remember, every time you get a Transfer object, like so:</p>
<code>
&lt;cfset emp = application.transfer.get("employee", 1)&gt;
</code>
<p>Transfer creates a CFC based on the XML definition of the employee type. There are times though when you may want to add a bit of business logic to this CFC. As a simple example, consider the act of getting a name from our employee. We have a first name and a last name, but what if we wanted a simple way to just get a name, perhaps based on: &quot;Lastname, First&quot;.</p>
<p>In order to do this we create a decorator. Begin by modifying the XML to tell Transfer where the decorator will exist:</p>
<code>
&lt;object name="employee" table="employees" decorator="empdir.model.employee"&gt;
</code>
<p>Remember empdir was defined in Application.cfc as a root level mapping for my sample application. I created a new folder named model and dropped a CFC in there named employee. At the simplest level, a decorator CFC can look like this:</p>
<code>
&lt;cfcomponent extends="transfer.com.TransferDecorator" output="false"&gt;
&lt;/cfcomponent&gt;
</code>
<p>Note that I extend transfer.com.TransferDecorator. This is the only requirement. Now if we want to add getName to our decorator, we can do:</p>
<code>
&lt;cffunction name="getName" access="public" returntype="string" output="false"&gt;
	&lt;cfreturn getLastName() &  ", " & getFirstName()&gt;
&lt;/cffunction&gt;
</code>
<p>Pretty simple, right? Here is a slightly more advanced example. Converting the DOB into an age value:</p>
<code>
&lt;cffunction name="getAge" access="public" returntype="numeric" output="false"&gt;
	&lt;cfreturn dateDiff("yyyy", getDOB(), now())&gt;
&lt;/cffunction&gt;
</code>
<p>Using it is just as simple as calling any of the 'normal' methods built into the TransferObject:</p>
<code>
&lt;cfset emp = application.transfer.get("employee", 1)&gt;
<p>&lt;cfoutput&gt;#emp.getName()# is #emp.getAge()# years old.&lt;br/&gt;&lt;/cfoutput&gt;
</code></p>
<p>This returns:</p>
<blockquote>
<p>
Camden, Raymond is 35 years old.
</p>
</blockquote>
<p>Along with overwriting get methods, you can overwrite set methods as well. In order to actually store the value you are overriding, you need to call getTransferObject() first. Here is a good example I'll steal from the <a href="http://docs.transfer-orm.com/wiki/Writing_Decorators.cfm">docs</a>:</p>
<code>
&lt;cffunction name="setHomePageURL" access="public" returntype="void" output="false"&gt;
	&lt;cfargument name="url" type="string" required="Yes"&gt;
	&lt;cfif not findNoCase("http://", arguments.url)&gt;
		&lt;cfset arguments.url = "http://" & arguments.url&gt;
	&lt;/cfif&gt;
	&lt;cfset getTransferObject().setHomePageURL(arguments.url)&gt;
&lt;/cffunction&gt;
</code>
<p>The setHomePageURL method defined here looks for a missing http://. If it doesn't exist in the passed in value then it will be added to the value. Notice then the last call uses getTransferObject() to &quot;hook&quot; into the real TO and call the real setHomePageURL.</p>
<p>For my employee decorator I decided to try something interesting. I want usernames to be unique. So why not do a security check when a username is set? I will follow these rules:</p>
<p>If no other employee has the same username, we are good.<br/>
If another employee has the same username, and isn't me, then we have an error.</p>
<p>Let's look at how I built this.</p>
<code>
&lt;cffunction name="setUsername" access="public" returntype="void" output="false"&gt;
	&lt;cfargument name="username" type="string" required="true"&gt;
<pre><code>&amp;lt;!--- see if another user with this name exists... ---&amp;gt;
&amp;lt;cfset var t = getTransfer()&amp;gt;
&amp;lt;cfset var olduser = t.readByProperty(&quot;employee&quot;, &quot;username&quot;, arguments.username)&amp;gt;

&amp;lt;!--- if it doesn't exist, we are good ---&amp;gt;
&amp;lt;cfif not olduser.getIsPersisted()&amp;gt;
	&amp;lt;cfset getTransferObject().setUsername(arguments.username)&amp;gt;
&amp;lt;!--- if it did exist, but was me, then its ok too ---&amp;gt;
&amp;lt;cfelseif olduser.sameTransfer(getTransferObject())&amp;gt;
	&amp;lt;cfset getTransferObject().setUsername(arguments.username)&amp;gt;
&amp;lt;!--- ok, throw an error! ---&amp;gt;
&amp;lt;cfelse&amp;gt;
	&amp;lt;cfthrow message=&quot;An employee with this username already exists.&quot;&amp;gt;
&amp;lt;/cfif&amp;gt;
</code></pre>
<p>&lt;/cffunction&gt;
</code></p>
<p>There is a lot going on here, so I'll tackle it line by line. I begin by calling getTransfer(). This method exists in decorators and gives me access to the main Transfer factory. I then do a read check using readByProperty. Remember that if readByProperty will return a virgin object if it doesn't exist. One of the special methods we can then use is getIsPersisted(). If this is false, it means olduser didn't actually exist, so we didn't find a match.</p>
<p>If it did exist, we can use another built in method, sameTransfer(), that compares two TransferObjects to see if they match.</p>
<p>If the above two checks fail, we throw an error. This was just a quick example and probably does <b>not</b> represent the best way to do validation in Transfer.</p>
<p>A cool example that my coworker <a href="http://www.corfield.org">Sean</a> came up was this:</p>
<code>
&lt;cffunction name="delete" returntype="void" access="public" output="false"&gt;
		&lt;cfset getTransfer().delete(this) /&gt;	
&lt;/cffunction&gt;
<p>&lt;cffunction name=&quot;save&quot; access=&quot;public&quot; returntype=&quot;void&quot; output=&quot;false&quot;&gt;
&lt;cfreturn getTransfer().save(getTransferObject())&gt;
&lt;/cffunction&gt;
</code></p>
<p>What does this do? Well remember how we use the Transfer Factory to save and delete objects? Well with these two methods in our decorator we can now do:</p>
<code>
&lt;cfset thirdemp = application.transfer.get("employee", 15)&gt;
&lt;cfset thirdemp.setLastName("Random#randRange(1,1000)#")&gt;
&lt;cfset thirdemp.save()&gt;
</code>
<p>This &quot;feels&quot; really natural to me. Sean actually built this into a separate CFC called a BaseDecorator that the other decorator's extended.</p>
<p>One more cool thing decorators can do - configuring. If you want to run custom code when Transfer creates an instance of as TransferObject, you can simply add a configure method to the decorator. For example:</p>
<code>
&lt;cffunction name="configure" access="private" returntype="void" output="false"&gt;
	&lt;cfset setLastName("anon")&gt;
	&lt;cfset setFirstName("anon")&gt;
&lt;/cffunction&gt;
</code>
<p>This will set the name of a new employee to anon, anon. Not terribly exciting but rather simple to use.</p>
<p>I'll leave you with another link from the main docs: <a href="http://docs.transfer-orm.com/wiki/How_to_Encrypt_User_Passwords_Using_a_Decorator.cfm">How to Encrypt User Passwords Using a Decorator</a>. This is a good example of using decorators and handling encryption.</p>
<p>I've updated the download zip to include the new code. As always, comments are welcome!<p><a href='enclosures/D%3A%5Chosts%5Cwww%2Ecoldfusionjedi%2Ecom%5Cenclosures%2Fempdirectory%2D2%2Ezip'>Download attached file.</a></p></p>
}