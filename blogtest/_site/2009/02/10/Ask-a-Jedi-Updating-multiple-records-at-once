<h1>Post</h1>

<p>Pema asks:</p>
<blockquote>
<p>
How can I update multiple records with single click of a button
in ColdFusion?
</p>
</blockquote>
<p>I had to double check with Pema to grok exactly what he meant, but basically, instead of the normal form where you edit one row of data, he had multiple rows of data and wanted to update them when the form was processed. This is pretty simple (advanced readers can skip ahead to the comments and make fun of my code) but he needed an example so I whipped something up.</p>
<!--more-->
<p>First, I wrote a template with a simple query and list of dynamic form fields:</p>
<pre><code class="language-markup">
&lt;cfquery name="getArt" datasource="cfartgallery" maxrows="10"&gt;
select	artid, artname
from	art
&lt;/cfquery&gt;

&lt;form action="test.cfm" method="post"&gt;
&lt;table&gt;
&lt;cfloop query="getArt"&gt;
	&lt;tr&gt;
		&lt;cfoutput&gt;&lt;td&gt;&lt;input type="text" name="art_#artid#" value="#artname#" /&gt;&lt;/td&gt;&lt;/cfoutput&gt;
	&lt;/tr&gt;
&lt;/cfloop&gt;
&lt;/table&gt;
&lt;input type="submit" name="save" value="Save" /&gt;
&lt;/form&gt;
</code></pre>
<p>My query grabbed the primary key and artname from the ColdFusion sample database cfartgallery. I create one text field for each row and pass the primary key value in the name of the form field.</p>
<p>Processing is rather simple as well:</p>
<pre><code class="language-markup">
&lt;cfif structKeyExists(form, "save")&gt;
	&lt;cfloop item="field" collection="#form#"&gt;
		&lt;cfif findNoCase("art_", field)&gt;
			&lt;cfset artid = listLast(field, "_")&gt;
			&lt;cfquery datasource="cfartgallery"&gt;
			update	art
			set		artname = &lt;cfqueryparam cfsqltype="cf_sql_varchar" value="#form[field]#"&gt;
			where	artid = &lt;cfqueryparam cfsqltype="cf_sql_integer" value="#artid#"&gt;
			&lt;/cfquery&gt;
 		&lt;/cfif&gt;
	&lt;/cfloop&gt;
&lt;/cfif&gt;
</code></pre>
<p>If the save button was clicked, I loop over the form. If the form field begins with art_, I can grab the primary key using a list function. Then I do an update.</p>
<p>That's it. Pretty simple, but hopefully it helps Pema and others who may be new to ColdFusion. I've included the complete template below.</p>
<pre><code class="language-markup">
&lt;cfif structKeyExists(form, "save")&gt;
	&lt;cfloop item="field" collection="#form#"&gt;
		&lt;cfif findNoCase("art_", field)&gt;
			&lt;cfset artid = listLast(field, "_")&gt;
			&lt;cfquery datasource="cfartgallery"&gt;
			update	art
			set		artname = &lt;cfqueryparam cfsqltype="cf_sql_varchar" value="#form[field]#"&gt;
			where	artid = &lt;cfqueryparam cfsqltype="cf_sql_integer" value="#artid#"&gt;
			&lt;/cfquery&gt;
 		&lt;/cfif&gt;
	&lt;/cfloop&gt;
&lt;/cfif&gt;

&lt;cfquery name="getArt" datasource="cfartgallery" maxrows="10"&gt;
select	artid, artname
from	art
&lt;/cfquery&gt;

&lt;form action="test.cfm" method="post"&gt;
&lt;table&gt;
&lt;cfloop query="getArt"&gt;
	&lt;tr&gt;
		&lt;cfoutput&gt;&lt;td&gt;&lt;input type="text" name="art_#artid#" value="#artname#" /&gt;&lt;/td&gt;&lt;/cfoutput&gt;
	&lt;/tr&gt;
&lt;/cfloop&gt;
&lt;/table&gt;
&lt;input type="submit" name="save" value="Save" /&gt;
&lt;/form&gt;
</code></pre>}