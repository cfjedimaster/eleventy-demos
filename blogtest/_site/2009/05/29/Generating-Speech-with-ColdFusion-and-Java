<h1>Post</h1>

<p>A few days ago a user made a comment on my ColdFusion 8/CAPTCHA blog post. He reminded us (and it is a good reminder) that CAPTCHA has some serious accessibility issues. This got me thinking about converting the CAPTCHA image into spoken letters. I've seen a few sites that do this and, frankly, whether it helped with CAPTCHA or not I thought it would be cool to see if ColdFusion could generate speech. I did some digging and the primary library that folks seem to use in the Java world is <a href="http://freetts.sourceforge.net/docs/index.php">FreeTTS</a> (TTS is short for text to speech). There are probably many other alternatives out there, but that's the one I went with.</p>
<p>I began by downloading the compiled code for FreeTTS and confirmed the example application ran from the command line. I then began to dig into the docs a bit. I then began to cry a little bit as I realized that &quot;documentation&quot; was probably too strong of a word for what I found at the project. The API is fully documented. Examples do exist. But I couldn't find anything close to what I'd consider to be good documentation. (Full disclosure time. I will admit to not always providing great docs with my own projects!) Specifically it wasn't difficult to get code that would say something. I had that running with 15 minutes. What took forever was getting the audio saved to a file. The code that follows works, but please note that the code could probably be done better.</p>
<p>Once you've downloaded the FreeTTS code, extract it to your file system. All you really need are the JAR files from the lib folder. I loaded all the JARs using the wonderful, super-awesome, <a href="http://javaloader.riaforge.org/">JavaLoader</a> from Mark Mandel. I <i>really</i> hope dynamic class loading comes to ColdFusion 9 because it's so darn useful. Here is how I used it to suck in all the JARs from the lib folder:</p>
<code>
&lt;cfset jardir = expandPath("./freetts-1.2.2-bin/freetts-1.2/lib")&gt;
&lt;cfset jars = []&gt;
&lt;cfdirectory name="jarList" directory="#jardir#"&gt;
&lt;cfloop query="jarList"&gt;
	&lt;cfset arrayAppend(jars, jardir & "/" & name)&gt;
&lt;/cfloop&gt;
<p>&lt;cfset loader = createObject(&quot;component&quot;, &quot;javaloader.JavaLoader&quot;).init(jars)&gt;
</code></p>
<p>Now for the fun part. FreeTTS works by creating a voice and having the voice speak. So at a basic level, this code alone will work to create the speech.</p>
<code>
&lt;cfset voiceManager = loader.create("com.sun.speech.freetts.VoiceManager")&gt;
&lt;cfset vm = voiceManager.getInstance()&gt;
&lt;cfset voice = vm.getVoice("kevin16")&gt;
<p>&lt;cfset voice.allocate()&gt;
&lt;cfset voice.speak(&quot;Hello World. This is a test of text to speech. It was a real pain in the ass. Really.&quot;)&gt;
</code></p>
<p>On my system this resulted in the words being spoken out of my laptop speakers. Did this surprise me. Heck yes. Did I scream like a little girl? I'm not telling. So as I said, this was relatively simple. Getting it to save to the file system though was a royal pain in the rear. Sure the code isn't that much different, it just took me forever to figure it out. The basic idea is to tell FreeTTS to use another audio player. FreeTTS has a 'player' called SingleFileAudionPlayer. As you can guess, this essentially turns a file into an audio player. In this version of the code, I set up the player and pass it to the voice. When run, it generates this wav file:</p>
<p><a href="http://www.raymondcamden.com/images/test1.wav">http://www.coldfusionjedi.com/images/test1.wav</a></p>
<p>I then switched the text to be something close to a CAPTHA. The result was a bit too quick to understand. Looking at the API, I saw that there was a WPM (words per minute) setting. You would think this would simply slow down the amount of words spoken per minute. Instead it simply slowed down the speech. So instead of hearing: &quot;Something ..... something ....&quot;. It was more like &quot;Sooooommmmmeeeething.&quot; I played with it a bit and got to be a bit slower, but, it's not perfect. Here is the final template I ended up with:</p>
<code>
&lt;cfset jardir = expandPath("./freetts-1.2.2-bin/freetts-1.2/lib")&gt;
&lt;cfset jars = []&gt;
&lt;cfdirectory name="jarList" directory="#jardir#"&gt;
&lt;cfloop query="jarList"&gt;
	&lt;cfset arrayAppend(jars, jardir & "/" & name)&gt;
&lt;/cfloop&gt;
<p>&lt;cfset loader = createObject(&quot;component&quot;, &quot;javaloader.JavaLoader&quot;).init(jars)&gt;</p>
<p>&lt;cfset audioFileFormatType = createObject(&quot;java&quot;, &quot;javax.sound.sampled.AudioFileFormat$Type&quot;).init(&quot;WAVE&quot;,&quot;wav&quot;)&gt;
&lt;cfset sfAudio = loader.create(&quot;com.sun.speech.freetts.audio.SingleFileAudioPlayer&quot;).init(&quot;/Library/WebServer/Documents/tts/test&quot;,audioFileFormatType)&gt;</p>
<p>&lt;cfset voiceManager = loader.create(&quot;com.sun.speech.freetts.VoiceManager&quot;)&gt;
&lt;cfset vm = voiceManager.getInstance()&gt;
&lt;cfset voice = vm.getVoice(&quot;kevin16&quot;)&gt;</p>
<p>&lt;cfset lex = loader.create(&quot;com.sun.speech.freetts.en.us.CMULexicon&quot;).init()&gt;
&lt;cfset voice.setLexicon(lex)&gt;
&lt;cfset voice.setRate(110)&gt;
&lt;cfset voice.setAudioPlayer(sfAudio)&gt;
&lt;cfset voice.allocate()&gt;
&lt;cfset voice.speak(&quot;A 9 ## 2 L K 8 0&quot;)&gt;
&lt;cfset sfAudio.close()&gt;</p>
<p>&lt;p&gt;
done
&lt;/p&gt;
</code></p>
<p>FreeTTS comes with more voices, and if I spent more time on it I could make it a bit nicer probably, but for now I'll stop and let folks comments. In the next blog entry, I'll show this in use with CAPTCHA.</p>
<p>As a reminder, in order for the template to work, you will need both JavaLoader and FreeTTS copied to your file system.</p>
}