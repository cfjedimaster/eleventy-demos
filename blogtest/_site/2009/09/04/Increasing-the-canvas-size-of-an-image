<h1>Post</h1>

<p>In ColdFusion, how would you increase the canvas size of an image without actually stretching the image? I played with this today while eating my peanut butter and jelly (PB&amp;J FTW) and came up with the following solution. The basic idea is pretty simple. You make a new image of the desired canvas size and then just paste in the old image. Here is a simple hard coded example.</p>
<!--more-->
<code>
&lt;cfset img = "/Users/ray/Pictures/darth.jpg"&gt;
&lt;cfset imgOb = imageNew(img)&gt;
<p>&lt;cfset newWidth = &quot;800&quot;&gt;
&lt;cfset newHeight = &quot;800&quot;&gt;</p>
<p>&lt;cfset newImg = imageNew(&quot;&quot;, newWidth, newHeight, &quot;rgb&quot;, &quot;white&quot;)&gt;</p>
<p>&lt;cfset imagePaste(newImg, imgOb, 0, 0)&gt;</p>
<p>&lt;!--- make it easy to see the image ---&gt;
&lt;body bgcolor=&quot;yellow&quot;&gt;
&lt;cfimage action=&quot;writeToBrowser&quot; source=&quot;#newImg#&quot;&gt;
</code></p>
<p>I begin with my source image that is read into a proper image variable. I create my canvas (newImg) with the desired size. Lastly, I paste it in. Because I used a white background I spit out a yellow background on the page to make it stand out a bit more.</p>
<p>Here is the result - and forgive me - I'm feeling lazy. I resized it for the web page via HTML. If you click you will see the full image in all its glory.</p>
<p><a href="http://www.raymondcamden.com/images/sep4.png"><img src="https://static.raymondcamden.com/images/cfjedi/sep4.png" border="0" width="400" height="400" /></a></p>
<p>It works, but what if we want to center the image in the canvas? We just need to add a bit of math.</p>
<code>
&lt;cfset img = "/Users/ray/Pictures/darth.jpg"&gt;
&lt;cfset imgOb = imageNew(img)&gt;
<p>&lt;cfset newWidth = &quot;800&quot;&gt;
&lt;cfset newHeight = &quot;800&quot;&gt;</p>
<p>&lt;cfset newImg = imageNew(&quot;&quot;, newWidth, newHeight, &quot;rgb&quot;, &quot;white&quot;)&gt;</p>
<p>&lt;!--- position it in the center ---&gt;
&lt;cfset centerX = (newWidth - imgOb.width)/2&gt;
&lt;cfset centerY = (newHeight - imgOb.height)/2&gt;</p>
<p>&lt;cfset imagePaste(newImg, imgOb, centerX, centerY)&gt;</p>
<p>&lt;!--- make it easy to see the image ---&gt;
&lt;body bgcolor=&quot;yellow&quot;&gt;
&lt;cfimage action=&quot;writeToBrowser&quot; source=&quot;#newImg#&quot;&gt;
</code></p>
<p>Woot. I'm so happy I took all those calculus classes in college. The result is now centered:</p>
<p><a href="http://www.coldfusionjedi.com/images/sep4a.png"><img src="https://static.raymondcamden.com/images/cfjedi/sep4a.png" width="400" height="400" border="0"/></a></p>
<p>Ok, so it works - but maybe we can make it a bit more abstract? Here is a simple UDF that lets you pass in an image, new dimensions, as well as a text-based position, and have it return the new image to you. It should probably use a dynamic color model, and it may barf if you use a smaller image, but it works with my simple tests.</p>
<code>
&lt;cffunction name="resizeCanvas" output="false"&gt;
	&lt;cfargument name="sourceImage" required="true" hint="Can be file, url, or image object."&gt;
	&lt;cfargument name="newWidth" required="true" type="numeric" hint="New canvas width."&gt;
	&lt;cfargument name="newHeight" required="true" type="numeric" hint="new canvas height."&gt;
	&lt;cfargument name="canvasbackgroundcolor" required="false" type="string" hint="BG color for canvas." default="white"&gt;
	&lt;cfargument name="position" required="false" type="string" hint="Where is the image positioned. Values are TL (top left), TR, BL, BR, Center" default="center"&gt;
<pre><code>&amp;lt;cfset var img = &quot;&quot;&amp;gt;
&amp;lt;cfset var canvas = &quot;&quot;&amp;gt;
&amp;lt;cfset var centerX = &quot;&quot;&amp;gt;
&amp;lt;cfset var centerY = &quot;&quot;&amp;gt;

&amp;lt;!--- first, is our sourceImage a string or an ob? ---&amp;gt;
&amp;lt;cfif not isImage(arguments.sourceImage)&amp;gt;
	&amp;lt;cfset img = imageNew(arguments.sourceImage)&amp;gt;
&amp;lt;cfelse&amp;gt;
	&amp;lt;cfset img = arguments.sourceImage&amp;gt;
&amp;lt;/cfif&amp;gt;

&amp;lt;!--- now make our canvas ---&amp;gt;
&amp;lt;cfset canvas = imageNew(&quot;&quot;, arguments.newWidth, arguments.newHeight, &quot;rgb&quot;, arguments.canvasbackgroundcolor)&amp;gt;

&amp;lt;!--- where we paste is based on position ---&amp;gt;
&amp;lt;cfswitch expression=&quot;#arguments.position#&quot;&amp;gt;

	&amp;lt;!--- Top Left ---&amp;gt;
	&amp;lt;cfcase value=&quot;TL&quot;&amp;gt;
		&amp;lt;cfset imagePaste(canvas, img, 0, 0)&amp;gt;
	&amp;lt;/cfcase&amp;gt;

	&amp;lt;!--- Top Right ---&amp;gt;
	&amp;lt;cfcase value=&quot;TR&quot;&amp;gt;
		&amp;lt;cfset imagePaste(canvas, img, arguments.newWidth - img.width, 0)&amp;gt;
	&amp;lt;/cfcase&amp;gt;

	&amp;lt;!--- Bottom Right ---&amp;gt;
	&amp;lt;cfcase value=&quot;BR&quot;&amp;gt;
		&amp;lt;cfset imagePaste(canvas, img, arguments.newWidth - img.width, arguments.newHeight - img.height)&amp;gt;
	&amp;lt;/cfcase&amp;gt;

	&amp;lt;!--- Bottom Left  ---&amp;gt;
	&amp;lt;cfcase value=&quot;BL&quot;&amp;gt;
		&amp;lt;cfset imagePaste(canvas, img, 0, arguments.newHeight - img.height)&amp;gt;
	&amp;lt;/cfcase&amp;gt;
	
	&amp;lt;!--- center ---&amp;gt;
	&amp;lt;cfdefaultcase&amp;gt;
		&amp;lt;cfset centerX = (newWidth - img.width)/2&amp;gt;
		&amp;lt;cfset centerY = (newHeight - img.height)/2&amp;gt;

		&amp;lt;cfset imagePaste(canvas, img, centerX, centerY)&amp;gt;
	&amp;lt;/cfdefaultcase&amp;gt;
	
&amp;lt;/cfswitch&amp;gt;

&amp;lt;cfreturn canvas&amp;gt;
</code></pre>
<p>&lt;/cffunction&gt;
</code></p>
<p>And some sample code to run it:</p>
<code>
&lt;cfset img = "/Users/ray/Pictures/darth.jpg"&gt;
<p>&lt;cfimage action=&quot;writeToBrowser&quot; source=&quot;#resizeCanvas(img,500,500,'blue')#&quot;&gt;
&lt;cfimage action=&quot;writeToBrowser&quot; source=&quot;#resizeCanvas(img,500,500,'blue','TR')#&quot;&gt;
&lt;cfimage action=&quot;writeToBrowser&quot; source=&quot;#resizeCanvas(img,500,500,'blue','BR')#&quot;&gt;
&lt;cfimage action=&quot;writeToBrowser&quot; source=&quot;#resizeCanvas(img,500,500,'blue','BL')#&quot;&gt;
</code></p>
<p>Which gives us this lovely montage:</p>
<p><a href="http://www.coldfusionjedi.com/images/sep4b.png"><img src="https://static.raymondcamden.com/images/cfjedi/sep4b.png" width="400" height="400" border="0"/></a></p>
<p>Hopefully this will be helpful for others. I may need to add this to the imageUtils project.</p>
}